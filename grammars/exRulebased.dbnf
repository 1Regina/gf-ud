-- texts and phrases
Text ::= Phr Punct # head punct
Text ::= Phr
Phr  ::= Utt

-- utterances and sentences
Utt ::= S
Utt ::= QS
Utt ::= NP
Utt ::= AP
Utt ::= IP
Utt ::= IAdv

S  ::= Cl
QS ::= QCl

-- clauses
Cl ::= NP VP  # nsubj head
Cl ::= NP do not VP  # nsubj aux advmod head
Cl ::= NP have VP  # nsubj aux head
Cl ::= NP have not VP  # nsubj aux advmod head
Cl ::= NP will VP  # nsubj aux head
Cl ::= NP will not VP  # nsubj aux advmod head
Cl ::= NP be V2 # nsubj:pass aux:pass head
Cl ::= there be_V NP # expl head nsubj
Cl ::= it be AP # expl cop head
Cl ::= NP be_V S_that # nsubj head ccomp


QCl ::= IP VP # nsubj head
QCl ::= do NP VP # aux nsubj head
QCl ::= V_aux NP VP # aux nsubj head
QCl ::= V_aux NP have VP # aux nsubj aux head
QCl ::= will NP VP # aux nsubj head
QCl ::= will NP have VP # aux nsubj aux head
QCl ::= IAdv do NP VP # advmod aux nsubj head
QCl ::= IAdv do NP not VP # advmod aux nsubj advmod head
QCl ::= IAdv V_aux NP VP # advmod aux nsubj head
QCl ::= IAdv V_aux NP not VP # advmod aux nsubj advmod head


VP ::= V
VP ::= V2 NP  # head obj
VP ::= V2 NP_obl # head obl
VP ::= V3 NP NP # head iobj obj
VP ::= V2C NP NP # head obj xcomp
VP ::= V2C NP AP # head obj xcomp
VP ::= VS S_that # head ccomp
VP ::= VV VP_to # head xcomp
VP ::= VV_aux VP # aux head
VP ::= be Comp  # cop head
VP ::= VP Adv # head advmod

S_that ::= S
S_that ::= that S # mark head

Comp ::= AP
Comp ::= NP
Comp ::= Adv
Comp ::= NP_obl

-- VP with to
VP_to ::= to VP # mark head

-- oblique noun phrase
NP_obl ::= Prep NP # case head

-- noun phrases
NP ::= PN
NP ::= CN
NP ::= Pron
NP ::= Det CN # det head
NP ::= NP NP_obl # head nmod
NP ::= Predet NP # det:predet head

-- a very special rule ---
NP ::= Det CN_quoted # det head
CN_quoted ::= quote CN quote # punct head punct

CN ::= N
CN ::= AP CN # amod head

IP ::= IDet CN # det head

-- adjectival phrases
AP ::= A
AP ::= AdA A   # advmod head
AP ::= AP NP_obl  # head nmod

-- adverbials
-- Adv ::= Prep NP # case head
Adv ::= Subj S # mark head

-- coordination
AP      ::= AP AP_conj         # head conj
AP      ::= AP AP_comma APConj # head conj conj
AP_conj  ::= Conj AP # cc head
AP_comma ::= comma AP # punct head 

-- POS tag configurations
#pos AUX be do have will VV_aux
#pos VERB V V2 V3 VS VV be_V
#pos NOUN N
#pos ADJ A
#pos PROPN PN
#pos ADV Adv IAdv AdA Predet not
#pos SCONJ Subj that if
#pos CConj Conj
#pos DET Det IDet
#pos PRON Pron IP there it
#pos PART to
#pos PUNCT Punct comma quote

-- structural word lexicon: normal categories
#token Prep at from in on to with
#token IP who whom what
#token IAdv when where why
#token AdA very too genetically
#token Det a an the every some many no
#token Pron I me you he him she her it we us they them
#token Subj if when although because
#token VV_aux can must
#token Punct . ! ?
#token Conj and or
#token IDet which

-- structural word lexicon: syncategorematic words
#token be am is are was were be been
#token be_V is are was were be been
#token do do does did
#token have have has had
#token will will would
#token not not
#token that that
#token to to
#token it it
#token there there
#token comma ,
#token quote "

-- content word lexicon
#token PN John Mary
#token V sleeps sleep slept walk walks walked sing sings sang sung
#token V2 sees see saw seen love loves loved look looks give gives gave
#token V3 give gives gave
#token V2C call calls called
#token VS say says believe believes
#token VV want wants try tries
#token A old good curious black best cold modified tired bad
#token N love sleep loves sleeps man men woman women cat cats doctor beer world elephant room hint hints reason reasons loser losers
#token Adv now today here
#token Predet probably only




